cmake_minimum_required(VERSION 3.16 FATAL_ERROR)
project(AOC_2024 LANGUAGES CXX)
enable_testing()
# specify the C++ standard
add_library(compiler_flags INTERFACE)
target_compile_features(compiler_flags INTERFACE cxx_std_23)
set(gcc_like_cxx "$<COMPILE_LANG_AND_ID:CXX,ARMClang,AppleClang,Clang,GNU,LCC>")
set(msvc_cxx "$<COMPILE_LANG_AND_ID:CXX,MSVC>")
target_compile_options(compiler_flags INTERFACE
  "$<${gcc_like_cxx}:$<BUILD_INTERFACE:-Wall;-Wextra;-Wshadow;-Wformat=2;-Wunused;-Werror>>"
  "$<${msvc_cxx}:$<BUILD_INTERFACE:-W3>>"
)

add_library(utils utils.cpp)
add_executable(utils_test utils.cpp)
target_include_directories(utils PRIVATE doctest)
target_include_directories(utils_test PRIVATE doctest)
target_compile_definitions(utils PRIVATE DOCTEST_CONFIG_DISABLE)
target_link_libraries(utils PUBLIC compiler_flags)
target_link_libraries(utils_test PUBLIC compiler_flags)
add_test(NAME Utils COMMAND utils_test)

function(day number part_one_solution part_two_solution)
    add_executable(day_${number} day_${number}.cpp)
    add_executable(day_${number}_test day_${number}.cpp)
    target_include_directories(day_${number} PRIVATE doctest)
    target_include_directories(day_${number}_test PRIVATE doctest)
    target_compile_definitions(day_${number} PRIVATE DOCTEST_CONFIG_DISABLE)
    target_link_libraries(day_${number} PRIVATE utils)
    target_link_libraries(day_${number}_test PRIVATE utils)
    target_link_libraries(day_${number} PUBLIC compiler_flags)
    target_link_libraries(day_${number}_test PUBLIC compiler_flags)

    add_test(NAME Day_${number} COMMAND day_${number}_test)
    add_test(NAME Day_${number}_Part_1 COMMAND day_${number} 1 ../inputs/day_${number}.txt)
    set_tests_properties(Day_${number}_Part_1 PROPERTIES PASS_REGULAR_EXPRESSION ^${part_one_solution}\n$)
    add_test(NAME Day_${number}_Part_2 COMMAND day_${number} 2 ../inputs/day_${number}.txt)
    set_tests_properties(Day_${number}_Part_2 PROPERTIES PASS_REGULAR_EXPRESSION ^${part_two_solution}\n$)
endfunction()

day(01 2166959 23741109)
day(02 686 717)
day(03 168539636 97529391)
day(04 2575 2041)
day(05 4662 5900)
day(06 5534 2262)
day(07 12553187650171 96779702119491)
day(08 303 1045)
day(09 6385338159127 6415163624282)
day(10 688 1459)
# day(11 186175 220566831337810)
day(12 1464678 877492)
day(13 40369 72587986598368)
day(14 218965032 7037)
day(15 1479679 1509780)
day(16 98484 531)
day(17 "1,3,5,1,7,2,5,1,6" 236555997372013)
day(18 454 "8,51")
day(19 304 705756472327497)
day(20 1406 1006101)
day(21 125742 157055032722640)
day(22 20401393616 2272)
day(23 926 "az,ed,hz,it,ld,nh,pc,td,ty,ux,wc,yg,zz")
day(24 55730288838374 "fvw,grf,mdb,nwq,wpq,z18,z22,z36")
day(25 TODO TODO)

file(DOWNLOAD https://github.com/doctest/doctest/releases/download/v2.4.11/doctest.h doctest/doctest.h EXPECTED_MD5 0b7fbd89a158063beecba78eb8400fad)
